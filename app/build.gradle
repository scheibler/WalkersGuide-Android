apply plugin: 'com.android.application'
//apply plugin: 'kotlin-android'
apply plugin: 'android-rocket-launcher'

// get signing properties
Properties keystoreProperties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    if (localProperties.getProperty('signingPropertiesFile') != null
            && new File(localProperties.getProperty('signingPropertiesFile')).exists()) {
        keystoreProperties.load(
                new FileInputStream(file(localProperties.getProperty('signingPropertiesFile'))))
    }
}
if (keystoreProperties.isEmpty()) {
    keystoreProperties.setProperty('storeFile', 'empty.keystore')
    keystoreProperties.setProperty('storePassword', 'storePassword')
    keystoreProperties.setProperty('keyAlias', 'keyAlias')
    keystoreProperties.setProperty('keyPassword', 'keyPassword')
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        resConfigs "en", "de"
        minSdkVersion 19
        targetSdkVersion 31
        multiDexEnabled true
        // contact data
        buildConfigField 'String', 'CONTACT_EMAIL', '"info@walkersguide.org"'
        buildConfigField 'String', 'CONTACT_WEBSITE', '"https://www.walkersguide.org"'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    signingConfigs {

        beta {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }

        prod {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    flavorDimensions "version"
    productFlavors {

        beta {
            applicationId "org.walkersguide.android.beta"
            versionCode 5
            versionName '2.0.0-b5'
            signingConfig signingConfigs.beta
            // internal
            // database
            buildConfigField 'String', 'DATABASE_NAME', '"walkersguide.db"'
            buildConfigField 'Integer', 'DATABASE_VERSION', '10'
            // server
            buildConfigField 'String', 'SERVER_URL', '"https://api.walkersguide.org/"'
            buildConfigField 'String', 'SERVER_URL_DEV', '"https://api.test.walkersguide.org/"'
            buildConfigField 'int[]', 'SUPPORTED_API_VERSION_LIST', '{3, 4}'
        }

        prod {
            applicationId "org.walkersguide.android"
            dimension "version"
            versionCode 18
            versionName '2.0.0'
            signingConfig signingConfigs.prod
            // internal
            // database
            buildConfigField 'String', 'DATABASE_NAME', '"walkersguide.db"'
            buildConfigField 'Integer', 'DATABASE_VERSION', '10'
            // server
            buildConfigField 'String', 'SERVER_URL', '"https://api.walkersguide.org/"'
            buildConfigField 'String', 'SERVER_URL_DEV', '"https://api.test.walkersguide.org/"'
            buildConfigField 'int[]', 'SUPPORTED_API_VERSION_LIST', '{3, 4}'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.flavorName}-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    lintOptions {
        disable "GoogleAppIndexingWarning", "Autofill", "IconLauncherShape", "ButtonStyle"
        abortOnError true
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    // kotlin
    //implementation 'androidx.core:core-ktx:1.7.0'
    //implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // androidX
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'

    // material design
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // google helpers
    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'com.google.code.gson:gson:2.8.9'

    // logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // public transport enabler
    implementation 'de.schildbach.pte:public-transport-enabler-2022-01-31@jar'
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
    implementation 'org.slf4j:slf4j-api:1.7.30'
}

repositories {
    // include local libs directory
    flatDir {
        dirs 'libs'
    }
}
