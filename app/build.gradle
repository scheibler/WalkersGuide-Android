apply plugin: 'com.android.application'

// get signing properties
Properties keystoreProperties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    if (localProperties.getProperty('signingPropertiesFile') != null
            && new File(localProperties.getProperty('signingPropertiesFile')).exists()) {
        keystoreProperties.load(
                new FileInputStream(file(localProperties.getProperty('signingPropertiesFile'))))
    }
}
if (keystoreProperties.isEmpty()) {
    keystoreProperties.setProperty('storeFile', 'empty.keystore')
    keystoreProperties.setProperty('storePassword', 'storePassword')
    keystoreProperties.setProperty('keyAlias', 'keyAlias')
    keystoreProperties.setProperty('keyPassword', 'keyPassword')
}

android {
    compileSdkVersion 36
    namespace "org.walkersguide.android"

    defaultConfig {
        versionCode 62
        versionName '3.2.3'
        minSdkVersion 21
        targetSdkVersion 36
        multiDexEnabled true
        resConfigs "en", "de", "uk"
        // when to show the ChangelogDialog
        // see the 'versionCode' variable above
        buildConfigField 'Integer', 'CHANGELOG_VERSION_CODE', '62'
        // contact data
        buildConfigField 'String', 'CONTACT_EMAIL', '"info@walkersguide.org"'
        buildConfigField 'String', 'CONTACT_WEBSITE', '"https://www.walkersguide.org"'
        // WalkersGuide server url
        buildConfigField 'String', 'SERVER_URL', '"https://api.walkersguide.org/"'
        buildConfigField 'String', 'SERVER_URL_DEV', '"https://api.test.walkersguide.org/"'
        // public-transport-enabler library
        buildConfigField 'String', 'PTE_LINK_MAIN_WEBSITE', '"https://github.com/schildbach/public-transport-enabler"'
        buildConfigField 'String', 'PTE_LINK_PROVIDER_LIST', '"https://github.com/schildbach/public-transport-enabler/tree/master/src/de/schildbach/pte"'
    }

    signingConfigs {

        beta {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }

        prod {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    flavorDimensions "version"
    productFlavors {

        beta {
            applicationId "org.walkersguide.android.beta"
            dimension "version"
            signingConfig signingConfigs.beta
            // internal database
            buildConfigField 'String', 'DATABASE_NAME', '"walkersguide.db"'
            buildConfigField 'Integer', 'DATABASE_VERSION', '13'
            // supported WalkersGuide server api versions
            buildConfigField 'int[]', 'SUPPORTED_API_VERSION_LIST', '{3, 4}'
        }

        prod {
            applicationId "org.walkersguide.android"
            dimension "version"
            signingConfig signingConfigs.prod
            // internal database
            buildConfigField 'String', 'DATABASE_NAME', '"walkersguide.db"'
            buildConfigField 'Integer', 'DATABASE_VERSION', '13'
            // supported WalkersGuide server api versions
            buildConfigField 'int[]', 'SUPPORTED_API_VERSION_LIST', '{3, 4}'
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "WalkersGuide-${variant.flavorName}-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    lintOptions {
        disable "GoogleAppIndexingWarning", "Autofill", "IconLauncherShape", "ButtonStyle"
        enable "NewerVersionAvailable"
        abortOnError true
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    // androidX
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core:1.17.0'
    implementation 'androidx.fragment:fragment:1.8.9'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    // material design
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // google helpers
    implementation 'com.google.guava:guava:33.5.0-android'
    implementation 'com.google.code.gson:gson:2.13.2'

    // logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // public transport enabler
    implementation 'de.schildbach.pte:public-transport-enabler'

    // test environment
    testImplementation 'junit:junit:4.13.2'
}

// Open the APK on your Android Device
ext {
    mainActivityClass = 'org.walkersguide.android.ui.activity.MainActivity'
}
apply from: 'gradle-scripts/openApkTask.gradle'

